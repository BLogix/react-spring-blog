buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:1.3.1'
    }
}

apply plugin: 'base'
apply plugin: 'com.moowork.node'

node {
    npmVersion = '2.1.5'
    yarnVersion = '0.16.1'
    distBaseUrl = 'https://nodejs.org/dist'
    download = true
}

yarn_build {
    inputs.files fileTree('public')
    inputs.files fileTree('src')

    inputs.file 'package.json'
    inputs.file 'yarn.lock'

    outputs.dir 'build'
}

assemble.dependsOn yarn_build

task packageYarnApp(type: zip){
    dependsOn yarn_build
    baseName 'yarn-app'
    extension 'jar'
    destinationDir file("${projectDir}/build_packageYarnApp")
    from('build'){
        into 'static'
    }
}

configurations {
    yarnResources
}

configurations.default.extendsFrom(configurations.yarnResources)

artifacts {
    yarnResources(packageYarnApp.archivePath) {
        builtBy packageYarnApp
        type "jar"
    }
}

assemble.dependsOn yarn_build //maybe take this line out if it won't build correctly
assemble.dependsOn packageYarnApp

String testsExecutedMarkerName = "${projectDir}/.tests.executed"

task test(type: YarnTask) {
    dependsOn assemble

    environment CI: 'true'
    args = ['run', 'test']
    inputs.files fileTree('src')
    inputs.file 'package.json'
    inputs.file 'yarn.lock'
    
    doLast {
        new File(testsExecutedMarkerName).text = 'delete this file to force re-execution JavaScript tests'
    }
    outputs.file testsExecutedMarkerName
}

check.dependsOn test

clean {
    delete packageYarnApp.archivePath
    delete testsExecutedMarkerName
}